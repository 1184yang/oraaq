How to Dequeue AQ exception queue

1.查詢 Queue 

    select name, queue_table, queue_type,
           enqueue_enabled, dequeue_enabled
      from user_queues
     where queue_table = 'HRC_TRN_QTAB'
    /
	
    NAME                QUEUE_TABLE   QUEUE_TYPE       ENQUE DEQUE
    ------------------- ------------- ---------------- ----- -----
    HRC_TRN_QUEUE       HRC_TRN_QTAB  NORMAL_QUEUE     YES   YES
    AQ$_HRC_TRN_QTAB_E  HRC_TRN_QTAB  EXCEPTION_QUEUE  NO    NO
	
	Exception Queue 通常預設都是 disabled 的,需要的時候才需要將它 enable。
	
2.查詢目前Queue Messages的狀況

    select count(*) count, msg_state, queue
      from AQ$HRC_TRN_QTAB
     group by msg_state, queue
	/
	
	 COUNT MSG_STATE            QUEUE
    ------ -------------------- --------------
        22 READY                HRC_TRN_QUEUE
        10 PROCESSED            HRC_TRN_QUEUE
		9  EXPIRED              AQ$_HRC_TRN_QTAB_E  <== 如果有Exception Message
		
3.Enable the exception queue for dequeue

   exec dbms_aqadm.start_queue('AQ$_HRC_TRN_QTAB_E', FALSE, TRUE);
   
4.Dequeue the messages on the exception queue

    set serveroutput on

    DECLARE
       enqueue_options dbms_aq.enqueue_options_t;
       message_properties dbms_aq.message_properties_t;
       dequeue_options dbms_aq.dequeue_options_t;
       message_handle RAW(16);
       mes HRC.HRC_TRN_TYP;
       no_messages EXCEPTION;
       pragma EXCEPTION_INIT (no_messages, -25228);

       cursor messages is
          select msg_id, user_data
            from AQ$HRC_TRN_QTAB
           where queue = 'AQ$_HRC_TRN_QTAB_E';
    BEGIN
       dequeue_options.wait := DBMS_AQ.NO_WAIT;
       dequeue_options.navigation := DBMS_AQ.FIRST_MESSAGE;

       for v_msg_id in messages loop
          dequeue_options.msgid := v_msg_id.msg_id;
          mes := v_msg_id.user_data;
          dbms_aq.dequeue(queue_name => 'AQ$_HRC_TRN_QTAB_E',
                          dequeue_options => dequeue_options,
                          message_properties => message_properties,
                          payload => mes,
                          msgid => message_handle);

          dbms_output.put_line('The message id is: ' || message_handle);

--          如果需要可 Re-enqueue the messages the normal notification
--          DBMS_AQ.ENQUEUE (
--             queue_name => 'HRC_TRN_QUEUE',
--             enqueue_options => enqueue_options,
--             message_properties => message_properties,
--             payload => mes,
--             msgid => message_handle);

          commit;

          dequeue_options.navigation := DBMS_AQ.NEXT_MESSAGE;

       end loop;
    EXCEPTION
       WHEN no_messages THEN
          DBMS_OUTPUT.PUT_LINE('No more messages');
       WHEN OTHERS THEN
          dbms_output.put_line('ERROR:');
          dbms_output.put_line(to_char(SQLCODE) || ': ' || SQLERRM);
    END;
    /

5.平常用不到, Disable the exception Queue
    
	exec dbms_aqadm.stop_queue('AQ$_HRC_TRN_QTAB_E', FALSE, TRUE, TRUE);
	